#+ endpoint: https://lod.globalbioticinteractions.org/query
#+ defaults:
#+   - uuid: urn:uuid:4fa7b334-ce0d-4e88-aaae-2e0c138d049e

SELECT DISTINCT
  ?doi
  ?uuid 
  ?contentId
  ?contentType
  ?archiveUrl
  ?seenAt
  ?provenanceId
  ?activity
WHERE  {
    BIND(?_uuid_iri as ?uuid) 
    BIND(?_type as ?contentType)
    BIND(<hash://sha256/5b7fa37bf8b64e7c935c4ff3389e36f8dd162f0705410dd719fd089e1ea253cd> as ?provenanceId)
  {
    SELECT ?contentId ?seenAt ?archiveUrl ?doi ?activity
    WHERE {
      graph ?g1 {
       ?_uuid_iri <http://www.w3.org/ns/prov#hadMember> ?archiveUrl .
       OPTIONAL { ?_uuid_iri <http://www.w3.org/1999/02/22-rdf-syntax-ns#seeAlso> ?doi . }
       ?archiveUrl <http://purl.org/dc/elements/1.1/format> ?_type .
      }
      graph ?activity {
        ?activity <http://www.w3.org/ns/prov#used> ?archiveUrl .
        ?activity <http://www.w3.org/ns/prov#generatedAtTime> ?seenAt .
        ?contentId <http://www.w3.org/ns/prov#qualifiedGeneration> ?activity .
        }
    }
  }
  # exclude blank nodes that represent unavailable or non-content
  FILTER(!CONTAINS(str(?contentId), ".well-known/genid" ) )

} ORDER BY DESC(?seenAt) LIMIT 2

